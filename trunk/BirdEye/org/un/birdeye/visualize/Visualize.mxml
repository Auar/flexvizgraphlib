<?xml version="1.0" encoding="utf-8"?>


<!--
 * The MIT License
 *
 * Copyright (c) 2007
 * United Nations Office at Geneva
 * Center for Visual Analytics
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 -->
		
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:vg="http://www.adobe.com/2006/fc"
	xmlns:un="org.un.flex.graphLayout.visual.*"
	xmlns:ui="org.un.birdeye.ui.*"
	xmlns:pr="org.un.flex.unComponentToolbox.renderers.*"
	xmlns:img="org.un.flex.unComponentToolbox.icons.EmbeddedIcons"
	xmlns:pi="com.flexibleexperiments.primitiveIcons.*"
	xmlns:mm="org.un.birdeye.visualize.minimap.*"
	creationComplete="init();" width="100%" height="100%" >

	<mx:Script>
		<![CDATA[
			import org.un.flex.graphLayout.visual.IEdgeRenderer;
			import mx.core.IDataRenderer;
			import mx.core.UIComponent;
			import org.un.flex.graphLayout.data.Graph;			
			import mx.rpc.events.ResultEvent;			
			import mx.collections.ArrayCollection;
			import mx.utils.ArrayUtil;
        	import mx.collections.XMLListCollection;
			import flash.events.*;
        	import mx.events.ItemClickEvent;  
        	
        	import org.un.flex.graphLayout.data.IGTree;
       		import org.un.flex.graphLayout.data.INode;
			import org.un.flex.graphLayout.visual.VisualNode;
			import org.un.flex.graphLayout.visual.IVisualNode;
			import org.un.flex.graphLayout.visual.IVisualGraph;
			import org.un.flex.graphLayout.visual.IEdgeRenderer;
			import org.un.flex.graphLayout.layout.ILayoutAlgorithm;
			import org.un.flex.graphLayout.data.IGraph;
			import org.un.flex.graphLayout.layout.ConcentricRadialLayouter;
			import org.un.flex.graphLayout.layout.SpringForceDirectedLayouter;
			import org.un.flex.graphLayout.layout.ForceDirectedLayouter;
			import org.un.flex.graphLayout.layout.ParentCenteredRadialLayouter;
			import org.un.flex.graphLayout.layout.PhylloTreeLayouter;
			import org.un.flex.graphLayout.layout.HierarchicalLayouter;
			import org.un.flex.graphLayout.visual.DefaultEdgeRenderer;
			import org.un.flex.graphLayout.visual.DirectedEdgeRenderer;
			import org.un.flex.graphLayout.visual.OrthogonalEdgeRenderer;
			import org.un.flex.graphLayout.visual.FlowEdgeRenderer;
			import org.un.flex.graphLayout.visual.VisualGraph;
			import org.un.birdeye.visualize.renderers.PrimitiveIconRenderer;
			import org.un.birdeye.images.EmbeddedIcons;
			
			import mx.events.ItemClickEvent;


			private var graph:IGraph;
			private var layouter:ILayoutAlgorithm;
			private var selectedEdgeRenderer:IEdgeRenderer;
			
			private var currNode:IVisualNode;
			private var prevNode:IVisualNode;
			private var startRoot:IVisualNode;
			private var itemCount:int = 0;

			private var initDone:Boolean = false;

			private var currentLayouterType:String = "";
			private var currentEdgeRendererType:String = "";

			private var rootNodeHistory:Array;
			private var currentHistoryIndex:uint;
			
			private var tree:IGTree;
			
			/* we need this more global anyway */
			private var xmlNames:Array;

			/* public attributes */
			[Bindable]
			public var scaleFactor:Number = 1;
			
			/* array collection to hold the XML data in order
			 * to allow searching and filtering */
			
			[Bindable]
			public var filterNodes:ArrayCollection;
			 
			[Bindable]
			public var selectedNodeID:String;
		
			/* methods */
			
				
			/* Init function, initiates the HTTP
			 * request. All further initalisation is done
			 * in the event handler for the HTTP Service */
			private function init():void {
				//SixDegreesExternalData.send();
				
				initData(['Node','Edge','fromID','toID']);
				leftAccordion.selectedIndex = 0;
				
			}
			
			/**
			 * event handler for HTTPService, does all the remaining required
			 * initialisation */
			private function initData(xN:Array):void {
				
				xmlNames = xN;
				
				/* first load the data into our array collection */
				initLocalData(parentDocument.rootNode.elements());
				
				/* init a graph object with the XML data */
				graph = new org.un.flex.graphLayout.data.Graph("XMLAsDocsGraph",false,parentDocument.rootNode,xmlNames);
				
				/* set the graph in the VGraph object, this automatically
				 * initializes the VGraph items */
				vgraph.graph = graph;

				/* set the default layouter type */
				currentLayouterType = "ConcentricRadial";
				currentEdgeRendererType = "Default";

				/* set the layouter */
				setLayouter();
				setEdgeRenderer();
				
				/* set the visibility limit options, default 2 */
				maxDistanceFromCurrent.value = 2;
				vgraph.maxVisibleDistance = maxDistanceFromCurrent.value;
				/* enable the history */
				// showHistory.selected = true;
				vgraph.showHistory = showHistory.selected;



				/* select a root node, most layouters requires a root node */
				startRoot = graph.nodeByStringId("1").vnode;

				/* init the root history array */
				rootNodeHistory = new Array;

				/* add it to the history */
				rootNodeHistory.push(startRoot);
				currentHistoryIndex = 0;
				
				/* set if edge labels should be displayed */
				vgraph.displayEdgeLabels = delCheckBox.selected;

				/* the following kicks it off .... */
				vgraph.currentRootVNode = startRoot;

				/* update UI */
				refreshUI();

				initDone = true;
				
				/* debug new XML feature */
				tree = graph.getTree(startRoot.node,false,true);
				parentDocument.gTreeXML = tree.XMLtree;
				}
			
				/* re-enable the refresh button in all cases */
				private function refresh():void {
				
				var oldroot:IVisualNode;
				var oldsid:String;
				var newroot:INode;
				
				/* remove and create a new layouter */			
				if(layouter != null) {
					layouter.resetAll();
					layouter = null;
				}
				
				/* init a graph object with the XML data */
				graph = new org.un.flex.graphLayout.data.Graph("XMLAsDocsGraph",false,parentDocument.rootNode,xmlNames);
				
				oldroot = vgraph.currentRootVNode;
				oldsid = oldroot.node.stringid;
				
				/* set the graph in the VGraph object, this automatically
				 * initializes the VGraph items */
				vgraph.graph = graph;
				
				setLayouter();
				
				/* setting a new graph invalidated our old root, we need to reset it */
				/* we try to find a node, that has the same string-id as the old root node */
				newroot = graph.nodeByStringId(oldsid);
				if(newroot != null) {
					startRoot = newroot.vnode;
					vgraph.currentRootVNode = startRoot; // this redraws the whole thing
				} else {
					throw Error("Cannot set a default root, bailing out");
				}
			}
			
			/**
			 * This function handles the change of layouter
			 */
			private function changeLayouter():void {
				
				currentLayouterType = (layoutSelector.selectedItem as String);
				
				/* kill off animation in old layouter if present */
				if(layouter != null) {
					layouter.resetAll();
				}
				
				/* first set the layouter */
				setLayouter();
				
				/* kick it off */
				vgraph.draw();
			}
			
			private function changeEdgeRenderer():void {
				
				currentEdgeRendererType = (edgeRendererSelector.selectedItem as String);
				
				/* kill off animation in old layouter if present 
				if(vgraph.edgeRenderer != null) {
					vgraph.edgeRenderer.resetAll();
				}
				*/
				
				/* first set the layouter */
				setEdgeRenderer();
				
				/* kick it off */
				vgraph.draw();
			}
			
			private function setLayouter():void {
				/* create and set the layouter */
				if(layouter != null) {
					layouter.resetAll(); // to kill any timers
				} 
				switch(currentLayouterType) {
					case "ConcentricRadial":
						layouter = new ConcentricRadialLayouter(vgraph);
						// What's not needed
						bsliderlabel.setStyle("color",0xCCCCCC);
						bslider.alpha=0.3;
						orientationSelector.alpha=0.3;
						orientationSelector.setStyle("color",0xCCCCCC);
						phidiallabel.setStyle("color",0xCCCCCC);
						phidial.alpha=0.3;
						break;
					case "ParentCenteredRadial":
						layouter = new ParentCenteredRadialLayouter(vgraph);
						(layouter as ParentCenteredRadialLayouter).phi = phidial.value;
						// What's not needed
						bsliderlabel.setStyle("color",0xCCCCCC);
						bslider.alpha=0.3;
						orientationSelector.alpha=0.3;
						orientationSelector.setStyle("color",0xCCCCCC);
						// What is needed
						phidiallabel.setStyle("color",0x000000);
						phidial.alpha=1;
						break;
					case "PhylloTree":
						layouter = new PhylloTreeLayouter(vgraph);
						(layouter as PhylloTreeLayouter).phi = phidial.value;
						// What's not needed
						bsliderlabel.setStyle("color",0xCCCCCC);
						bslider.alpha=0.3;
						orientationSelector.alpha=0.3;
						orientationSelector.setStyle("color",0xCCCCCC);
						// What is needed
						phidiallabel.setStyle("color",0x000000);
						phidial.alpha=1;
						break;
					case "ForceDirected":
						layouter = new SpringForceDirectedLayouter(vgraph);
						layouter.linkLength = linkLength.value;
						// What's not needed
						bsliderlabel.setStyle("color",0xCCCCCC);
						bslider.alpha=0.3;
						orientationSelector.alpha=0.3;
						orientationSelector.setStyle("color",0xCCCCCC);
						phidiallabel.setStyle("color",0xCCCCCC);
						phidial.alpha=0.3;
						break;
					case "Hierarchical":
						layouter = new HierarchicalLayouter(vgraph);
						(layouter as HierarchicalLayouter).breadth = bslider.value;
						(layouter as HierarchicalLayouter).enableSiblingSpread = siblingSpread.selected;
						// What's not needed
						phidiallabel.setStyle("color",0xCCCCCC);
						phidial.alpha=0.3;
						// What is needed
						bsliderlabel.setStyle("color",0x000000);
						bslider.alpha=1;
						orientationSelector.alpha=1;
						orientationSelector.setStyle("color",0x000000);
						break;
					case "NitinForce":
						layouter = new ForceDirectedLayouter(vgraph);
						layouter.linkLength = linkLength.value;
						// What's not needed
						bsliderlabel.setStyle("color",0xCCCCCC);
						bslider.alpha=0.3;
						orientationSelector.alpha=0.3;
						orientationSelector.setStyle("color",0xCCCCCC);
						phidiallabel.setStyle("color",0xCCCCCC);
						phidial.alpha=0.3;
						break;
					default:
						layouter = new ConcentricRadialLayouter(vgraph);
						
						
				}
				vgraph.layouter = layouter;
				layouter.autoFitEnabled = autoFit.selected;
				
			}
			
		
			
			private function setEdgeRenderer():void {
				var selectedEdgeRenderer:IEdgeRenderer;
				
				switch(currentEdgeRendererType) {
					case "Default":
						selectedEdgeRenderer = new DefaultEdgeRenderer;
						vgraph.edgeRenderer = selectedEdgeRenderer;
						break;
					case "Directed":
						selectedEdgeRenderer = new DirectedEdgeRenderer;
						vgraph.edgeRenderer = selectedEdgeRenderer;
						break;
					case "Orthogonal":
						selectedEdgeRenderer = new OrthogonalEdgeRenderer;
						vgraph.edgeRenderer = selectedEdgeRenderer;
						break;
					case "Flow":
						selectedEdgeRenderer = new FlowEdgeRenderer;
						vgraph.edgeRenderer = selectedEdgeRenderer;
						break;
					default:
						selectedEdgeRenderer = new DefaultEdgeRenderer;;
						vgraph.edgeRenderer = selectedEdgeRenderer;
			
				}
			}
			
		
			
			/**
			 * this function is called on any vgraphChanged event
			 * so that some UI elements like lables could be
			 * updated */
			private function refreshUI():void {
				if(infoText != null) {
					infoText.text = vgraph.currentRootVNode.data.@name;
				}
				
				linkLength.value = layouter.linkLength;
				
				if(layouter is HierarchicalLayouter) {
					bslider.value = (layouter as HierarchicalLayouter).breadth;
				
				}
				
				novisnodeslabel.text = vgraph.noVisibleVNodes.toString();
			}
			
			/* we visually indicate the available/unavailable status of the back/forward buttons
			by using a reduced alpha to indicate not available. */
			private function getButtonAlpha(available:Boolean):Number {
				return available ? 0.9 : 0.6;
			}
			
			/* this updates the scale factor in all children of the vgraph
			 * it could be moved into the vgraph... */
			private function updateScale():void {
				var children:Array;
				var child:UIComponent;
				
				children = vgraph.getChildren();
				for each(child in children) {
					if(child is PrimitiveIconRenderer) {
						child.scaleX = scaleSlider.value;
						child.scaleY = scaleSlider.value;
					}
				}
				layouter.layoutChanged = true;
				vgraph.draw();
			}
			
			private function enableSiblingSpread():void {
				if(layouter is HierarchicalLayouter) {
					(layouter as HierarchicalLayouter).enableSiblingSpread = siblingSpread.selected;
					layouter.layoutChanged = true;
					vgraph.draw();
				}
			}
			
			private function setOrientation():void {
				var or:String;
				if(layouter is HierarchicalLayouter) {
					or = orientationSelector.selectedItem as String;
					switch(or) {
						case "Top Down":
							(layouter as HierarchicalLayouter).orientation = 
								(layouter as HierarchicalLayouter).ORIENT_TOP_DOWN;
							break;
						case "Bottom Up":
							(layouter as HierarchicalLayouter).orientation = 
								(layouter as HierarchicalLayouter).ORIENT_BOTTOM_UP;
							break;
						case "Left-Right":
							(layouter as HierarchicalLayouter).orientation = 
								(layouter as HierarchicalLayouter).ORIENT_LEFT_RIGHT;
							break;
						case "Right-Left":
							(layouter as HierarchicalLayouter).orientation = 
								(layouter as HierarchicalLayouter).ORIENT_RIGHT_LEFT;
							break;
						default:
							throw Error("Illegal orientation selected:"+or);
							break;
					}
					layouter.layoutChanged = true;
					vgraph.draw();
				}
			}
			
			/* this function takes the node and selects it as a root
			 * node, automatically centering the layout around it */
			private function centerNode(nodeID:String):void {
				/* we assume we have the vgraph and graph objects */
				
				var newroot:INode;
				
				newroot = graph.nodeByStringId(nodeID);
				
				/* if we have a node, set its vnode as the new root */
				if(newroot) {
					
					/* is it really a new node */
					if(newroot.vnode != vgraph.currentRootVNode) {
						
						/* set it in the vgraph */
						vgraph.currentRootVNode = newroot.vnode;
						
						/* add it to the history */
						rootNodeHistory.push(newroot.vnode);
						
						
					}
				} else {
					trace("Node with id:"+nodeID+" not found!!");
				}
			}
 
 			/* update the linklength, this indicator always
 			 * goes from 0 to 100, depending on the layouter
 			 * this is translated to something meaningful */
			private function updateLinkLength():void {
				layouter.linkLength = linkLength.value;
				vgraph.draw();
			}
			
			/* this is specific to the ParentCentered
			 * layouter only */
			private function updatePhi():void {
				
				if(layouter is ParentCenteredRadialLayouter) {
					(layouter as ParentCenteredRadialLayouter).phi = phidial.value;
					vgraph.draw();
				}
				else if(layouter is PhylloTreeLayouter) {
					(layouter as PhylloTreeLayouter).phi = phidial.value;
					vgraph.draw();
				}
				/* else NOP */
			}
			
			/* this is for hierarchical layout only */
			private function updateNodeSpacing():void {
				if(layouter is HierarchicalLayouter) {
					(layouter as HierarchicalLayouter).breadth = bslider.value;
					vgraph.draw();
				}
			}
			
			private function updateAutoFit():void {
				layouter.autoFitEnabled = autoFit.selected;
				vgraph.draw();
			}

			private function changeMaxDistance():void {
				vgraph.maxVisibleDistance = maxDistanceFromCurrent.value;
				vgraph.draw();
			}

			private function changeShowHistory():void {
				vgraph.showHistory = showHistory.selected;
			}
 			
 			private function displayEdgeLabels():void {
 				vgraph.displayEdgeLabels = delCheckBox.selected;
 			}
 			
 			private function changeDamping():void {
 				if(layouter is SpringForceDirectedLayouter) {
 					(layouter as SpringForceDirectedLayouter).dampingActive = dampCheck.selected;
 				}
 			}
 	
 	 
 			/**
 			 * handle a resize event, but only do anything if the init was
 			 * done
 			 */
 			private function handleResize():void {
 				if(initDone) {
 					vgraph.draw();
 				}
 			}
 
			/**
			 * initialises the 'filterNodes' array collection in order 
			 * to allow filtering and sorting etc. */
 			private function initLocalData(result:XMLList):void {
 				var arr:Array = new Array();
 				
				for (var i:uint = 0; i < result.length(); ++i) {
				    arr[i] = result[i];
				}
				filterNodes = new ArrayCollection(arr);
				filterNodes.filterFunction = processFilter;
 			}
 			
 			/**
 			 * function to implement a filter function on the
 			 * "filterNodes" ArrayCollection that holds our XML data
 			 * locally */
 			private function processFilter(item:Object):Boolean {

				var name:String;
				
				name = item.@name;

				// If no filter text, or a match, then true 
				if(!name.length || 
					name.toUpperCase().indexOf(txtFilter.text.toUpperCase()) >= 0) {
					return true;
				} 
				return false;
			}
		
			/**
			 * local double click, note that the node is
			 * made root node anyway from the vgraph itself
			 * but we have to add it to the history
			 */
			public function itemDoubleClick(e:Event):void {
				var vn:IVisualNode;
				var dr:IDataRenderer;
				
				dr = (e.currentTarget as IDataRenderer);
				
				if(dr) {
					vn = (dr.data as IVisualNode);
					rootNodeHistory.push(vn);
					// trace("added node:"+vn.id+" to history");
				}
			}

			public function singleClick(e:Event): void {
				var ir:IDataRenderer;
				
				if(e.currentTarget is IDataRenderer) {
					ir = (e.currentTarget as IDataRenderer);
					
					leftAccordion.selectedIndex = 2;
					
				} else {
					trace("single click event target is no IDataRenderer");
				}
			}
		
		
			/**
			 * move backwards in the history */
			public function historyBack():void {
				var hl:uint;
				var vn:IVisualNode;
				
				hl = rootNodeHistory.length;
				
				if(currentHistoryIndex < hl - 1) {
					++currentHistoryIndex;
					
					vn = rootNodeHistory[currentHistoryIndex];
					/* set the root node as the one in the 
					 * history */
					if(vn != vgraph.currentRootVNode) {
						vgraph.currentRootVNode = vn;
					}
				}
			}
			
			/**
			 * move forward */
			public function historyForward():void {
				var vn:IVisualNode;
				if(currentHistoryIndex > 0) {
					--currentHistoryIndex;
					vn = rootNodeHistory[currentHistoryIndex];
					/* set the root node as the one in the 
					 * history */
					if(vn != vgraph.currentRootVNode) {
						vgraph.currentRootVNode = vn;
					}
				}
			}
			
			public function histForwardOk():Boolean {
				return (currentHistoryIndex > 0);
			}
			
			public function histBackOk():Boolean {
				var hl:uint;
				hl = rootNodeHistory.length;
				return(currentHistoryIndex < hl - 1);
			}
		
		]]>
	</mx:Script>					

	<!-- Effects -->
	<mx:WipeLeft id="wipeLeft" duration="1000" />
	<mx:WipeRight id="wipeRight" duration="1000" />
	
	<mx:HBox id="DivNavRB" width="100%" height="100%" paddingBottom="5" horizontalGap="5">
		<mx:HBox id="sideBar" width="300" height="100%" horizontalGap="0" >
			<ui:Grip id="navGrip" 
				width="17" height="100%" horizontalAlign="center"
				gripTip="Show/Hide Control Panel" />
			<mx:VBox id="NavContainerRB" horizontalScrollPolicy="off" verticalScrollPolicy="off" verticalAlign="middle" horizontalAlign="center" verticalGap="1" horizontalGap="0" width="100%" height="100%" hideEffect="{wipeLeft}" showEffect="{wipeRight}">
				<mx:Accordion id="leftAccordion" paddingBottom="5" paddingRight="5" paddingTop="5" paddingLeft="5" width="100%" height="100%" creationPolicy="all"  >
					<mx:Canvas id="visibilitycnvs" label="Visibility/Layout" height="100%" width="100%" paddingBottom="5" >
						<mx:VBox width="100%" horizontalAlign="center">
							<mx:Spacer height="3"/>
								
								<!-- Graph and Layout Parameters -->
								
								<mx:VBox width="100%" horizontalAlign="center" paddingLeft="5" paddingRight="5" paddingBottom="5" paddingTop="5" borderColor="#666666" borderThickness="1" cornerRadius="5" borderStyle="solid" backgroundColor="#CCCCCC" backgroundAlpha=".2" >
								<mx:Text text="Graph Layout Controls" width="100%" fontSize="10" fontWeight="bold" textAlign="center" />
								
								<mx:ComboBox id="layoutSelector" prompt="Select layout algorithm" width="95%" toolTip="Some layouts are under construction" change="changeLayouter()" >
									<mx:ArrayCollection>
										<mx:String>ConcentricRadial</mx:String>
										<mx:String>ParentCenteredRadial</mx:String>
										<mx:String>SingleCycleCircle</mx:String>
										<mx:String>Hierarchical</mx:String>
										<mx:String>Hyberbolic</mx:String>
										<mx:String>ISOM</mx:String>
										<mx:String>Flow</mx:String>
										<mx:String>PhylloTree</mx:String>
										<mx:String>ForceDirected</mx:String>
										<mx:String>NitinForce</mx:String>
									</mx:ArrayCollection>
								</mx:ComboBox>	
															
							<mx:HBox width="100%" >
							<mx:Label text="Center Node: " />
							<mx:Text  text="no data loaded" id="infoText" fontSize="9" textAlign="left" width="100%" />
							</mx:HBox>
							<mx:HBox width="100%" >
								<mx:Label text="Visible Nodes:" fontSize="9" textAlign="left"/>
								<mx:Text id="novisnodeslabel" text="unknown" fontSize="9" textAlign="left"/>
							</mx:HBox>
							<mx:HRule width="95%"/>
							
							<mx:Label id="bsliderlabel" toolTip="Change the node spacing in Hierarchical" text="Hierarchical Node Spacing" fontSize="9"  textAlign="center"/>
							<mx:HSlider tickInterval="20" width="95%" id="bslider" value="160" minimum="0" maximum="200"
								liveDragging="true" change="updateNodeSpacing()" />
							<mx:ComboBox id="orientationSelector" color="#CCCCCC" width="95%" prompt="Select the orientation" toolTip="Select the orientation of tree layout" change="setOrientation()" >
									<mx:ArrayCollection>
										<mx:String>Top Down</mx:String>
										<mx:String>Bottom Up</mx:String>
										<mx:String>Left-Right</mx:String>
										<mx:String>Right-Left</mx:String>
									</mx:ArrayCollection>
								</mx:ComboBox>	
							<mx:Label id="phidiallabel" toolTip="This control changes the geometric angle of ParentCentered and PhylloTree Layouts" text="Layout Angle" fontSize="9"  textAlign="center"/>
							<mx:HSlider tickInterval="30" width="95%" id="phidial" value="160" minimum="-360" maximum="360"
								liveDragging="true" change="updatePhi()" />
							<mx:HBox>
								<mx:CheckBox selected="true" id="dampCheck" label="Damping" toolTip="Enable/Disable damping"
									click="changeDamping()" />
								<mx:CheckBox selected="true" id="siblingSpread" label="Sibling Spread" toolTip="Enable/Disable Sibling Spread"
									click="enableSiblingSpread()" />
							</mx:HBox>
							
							</mx:VBox>
							
							<!-- Annotations -->
							
							<mx:VBox width="100%" horizontalAlign="center" paddingLeft="5" paddingRight="5" paddingBottom="5" paddingTop="5" borderColor="#666666" borderThickness="1" cornerRadius="5" borderStyle="solid" backgroundColor="#CCCCCC" backgroundAlpha=".2" >
							<mx:Text text="Style and Annotations" width="100%" fontSize="10" fontWeight="bold" textAlign="center" />
							<mx:HBox width="100%" >
							<mx:ComboBox id="edgeRendererSelector" prompt="Select edge style" width="95%" toolTip="Select the style of line drawn among nodes" change="changeEdgeRenderer()"  >
									<mx:ArrayCollection>
										<mx:String>Default</mx:String>
										<mx:String>Directed</mx:String>
										<mx:String>Orthogonal</mx:String>
										<mx:String>Flow</mx:String>
										<mx:String>Belzier Curve</mx:String>
									</mx:ArrayCollection>
							</mx:ComboBox>
							<mx:CheckBox selected="true" toolTip="Select to show edge labels."
									id="delCheckBox" label="Edge Labels" click="displayEdgeLabels()" />
							</mx:HBox>
							</mx:VBox>
							
							<!-- View Controls -->
							
							<mx:VBox width="100%" horizontalAlign="center" paddingLeft="5" paddingRight="5" paddingBottom="5" paddingTop="5" borderColor="#666666" borderThickness="1" cornerRadius="5" borderStyle="solid" backgroundColor="#CCCCCC" backgroundAlpha=".2" >
							<mx:Text text="View Controls" width="100%" fontSize="10" fontWeight="bold" textAlign="center" />
							
							<mx:Label toolTip="Use this control to specify link level" text="Degrees of Separation" fontSize="9" textAlign="center" />
							<mx:HSlider tickInterval="1" width="95%" id="maxDistanceFromCurrent" value="2" minimum="1" maximum="10" 
								liveDragging="true" snapInterval="1" change="changeMaxDistance()" />
							<pi:PrimitiveRectangle height="10" width="95%" enabled="false"
								includeInLayout="true"
								fillEnabled="true" fillAngle="0" fillType="linear" fillFocalPointRatio="0" 
								fillSpreadMethod="pad" fillColors="[#00cc00,#ffff00,#ff9900,#ff0000]" 
								fillAlphas="[0.6,1,1,1]" fillRatios="[0,127,174,197]" 
								fillSelectedColors="[#ff6600,#ffffcc,#ffffcc,#ffffcc]"
								toolTip="Increasing degrees of separation requires significant processing power; 4 or more degrees may cause performance issues"					
							/>
							
							<mx:Label toolTip="Use this control to length of links.  Used when AutoSize is disabled." text="Link Length" fontSize="9"  textAlign="center"/>
							<mx:HBox width="95%" >
							<mx:Button id="minusLinkLength" icon="@Embed('/org/un/birdeye/images/ui/zoomout.png')" cornerRadius="5" click="scaleSlider.value=scaleSlider.value-.10;" />	
							<mx:HSlider tickInterval="25" width="95%" id="linkLength" value="100" minimum="0" maximum="600"
								liveDragging="true" change="updateLinkLength()" />
							<mx:Button id="plusLinkLength" icon="@Embed('/org/un/birdeye/images/ui/zoomin.png')" cornerRadius="5" click="scaleSlider.value=scaleSlider.value+.10;" />
							</mx:HBox>	
							<mx:Label toolTip="Use this control to increase/decrease text size" text="Text Scaling" fontSize="9" textAlign="center" />
							
							<mx:HBox width="95%" >
							<mx:Button id="minusScale" icon="@Embed('/org/un/birdeye/images/ui/zoomout.png')" cornerRadius="5" click="scaleSlider.value=scaleSlider.value-.10;" />	
							<mx:HSlider id="scaleSlider" value="1" minimum="0.5" maximum="2.0" liveDragging="true" width="95%" tickInterval="0.25" snapInterval="0.1"
									change="updateScale();"/>
							<mx:Button id="plusScale" icon="@Embed('/org/un/birdeye/images/ui/zoomin.png')" cornerRadius="5" click="scaleSlider.value=scaleSlider.value+.10;" />
							</mx:HBox>
							
							<mx:Label toolTip="Use this control to height of birdeye view" text="Birdeye Zoom:" fontSize="9" textAlign="center" />
							<mx:HBox width="95%" >
							<mx:Button id="minusZoom" icon="@Embed('/org/un/birdeye/images/ui/zoomout.png')" cornerRadius="5" click="zoom.value=zoom.value-.10;" />	
							<mx:HSlider 
								id="zoom" width="95%" tickInterval=".25"
								value="1" 
								minimum=".25" 
								maximum="2" 
								liveDragging="true" />
							<mx:Button id="plusZoom" icon="@Embed('/org/un/birdeye/images/ui/zoomin.png')" cornerRadius="5" click="zoom.value=zoom.value+.10;" />
							
							</mx:HBox>
							
							<mx:HBox>
								<mx:CheckBox selected="true" toolTip="Select to restrict graph to boundaries of current view space.  Graph will autofit its size so that all nodes are visible."
									id="autoFit" label="AutoFit" click="updateAutoFit()" />
								<mx:CheckBox selected="false" id="showHistory" label="History" toolTip="Select will retain visibility of all nodes visited"
									click="changeShowHistory()"/>
							</mx:HBox>
							</mx:VBox>
							
							
							<mx:Button id="fromSixDegreesDataButton" width="100%" label="Refresh Data" toolTip="Refresh data to reflect database changes..."
								click="refresh()" fontSize="9" icon="@Embed(source='/org/un/birdeye/images/ui/refresh.png')" />
							
							<mx:Spacer height="100%" />
							
						</mx:VBox>		
					</mx:Canvas>
					<mx:Canvas id="searchcnvs" label="Search/Filter Nodes" height="100%" width="100%">
						<mx:VBox width="100%" height="100%" paddingLeft="5" paddingRight="5">
							<mx:HBox width="100%" verticalAlign="middle" >
							<mx:Image source="@Embed(source='/org/un/birdeye/images/ui/search.png')" />
							<mx:Label text="Enter Search Criteria:"/>
							</mx:HBox>
							
							<mx:TextInput 
								id="txtFilter" 
								width="100%"
								change="filterNodes.refresh()"/>
							<mx:HRule width="95%"/>
									
							<!-- Use DataGrid Click to call graph dataprovider() function to center node -->
							<mx:DataGrid id="dgSearch" doubleClick="centerNode(dgSearch.selectedItem.@id)" width="100%" height="100%" 
								dataProvider="{filterNodes}" doubleClickEnabled="true" toolTip="Please double click to center" >
								<mx:columns>
									<mx:DataGridColumn id="nodeTitle" headerText="Node" dataField="@name" />
								</mx:columns>
							</mx:DataGrid>
						</mx:VBox>
					</mx:Canvas>
					<mx:Canvas id="detailcnvs" label="Details" height="100%" width="100%">
						<mx:VBox width="100%" height="100%">
							<mx:Spacer height="5" width="100%"/>
							<mx:VBox width="100%" height="100%" paddingLeft="5" paddingRight="5" paddingBottom="5" id="myVBox" borderColor="#666666" borderThickness="1" cornerRadius="5" borderStyle="solid" backgroundColor="#CCCCCC" backgroundAlpha=".2" >
								<mx:Label text="Title" fontWeight="bold"/>
								<mx:TextArea id="detailTitle" borderStyle="none" backgroundAlpha=".1" width="100%" editable="false" wordWrap="true" text="" />	
								<mx:Label text="Description" fontWeight="bold"/>
								<mx:TextArea id="detailDesc" verticalScrollPolicy="off" borderStyle="none" backgroundAlpha=".1" editable="false" width="100%" height="100%" wordWrap="true" text=""/>
							</mx:VBox>
							<!--
							<mx:Button label="Remove Node from View" icon="@Embed('/imgs/detailclose2.gif')" width="100%" toolTip="Will remove node from graph.  Will not be deleted from database." click="removeNode(selectedNodeID)" />
							-->
							
						</mx:VBox>
					</mx:Canvas>
					<mx:Canvas id="savedcnvs" label="Saved Graphs" height="100%" width="100%">
						<mx:VBox width="100%" height="100%" paddingLeft="5" paddingRight="5">
							<mx:Spacer height="3"/>
							<mx:VBox width="100%" paddingLeft="5" paddingRight="5" paddingBottom="5" paddingTop="5" borderColor="#666666" borderThickness="1" cornerRadius="5" borderStyle="solid" backgroundColor="#CCCCCC" backgroundAlpha=".2" >
							<mx:Text text="Save Current Graph" width="100%" fontSize="10" fontWeight="bold" textAlign="center" />
							<mx:Label text="Graph Name:"  />
							<mx:TextInput width="100%"  />
							<mx:Button label="Save" width="100%"  />	
							</mx:VBox>
							<mx:Spacer height="3"/>
							<mx:Text text="Load Saved Graph" width="100%" fontSize="10" fontWeight="bold" textAlign="center" />
							<mx:DataGrid id="dgSavedGraphs" width="100%" height="100%" 
								dataProvider="{filterNodes}" toolTip="Please double click to draw saved graph" >
								<mx:columns>
									<mx:DataGridColumn id="graphName" headerText="Node" dataField="@name" />
								</mx:columns>
							</mx:DataGrid>
							
							
							
						</mx:VBox>
					</mx:Canvas>
				</mx:Accordion>
				
			</mx:VBox>		
		</mx:HBox>

		<!-- Graph Panel -->
		<mx:Canvas id="graphCnvs" borderStyle="solid" borderColor="#CCCCCC" width="100%" height="100%"  >		
			
			<un:VisualGraph id="vgraph" width="100%" height="100%" 
				paddingBottom="5" left="0" right="0" top="0" bottom="0" backgroundColor="#FFFFFF" alpha="1"
				itemRenderer="org.un.birdeye.visualize.renderers.PrimitiveIconRenderer" 
				edgeLabelRenderer="org.un.birdeye.visualize.renderers.edgeRenderer"
				vgraphChanged="refreshUI()"
				resize="handleResize()"
				visibilityLimitActive="true"
				scaleX="{ zoom.value }" 
				scaleY="{ zoom.value }"
				>
			</un:VisualGraph>
			
		</mx:Canvas>
		<!-- End of Graph Panel -->
	</mx:HBox>
	
	<mx:states>
       <mx:State name="showCntrlPanel">
			<mx:SetProperty target="{sideBar}" name="width" value="300"/>
			<mx:SetStyle target="{DivNavRB}" name="horizontalGap" value="5"/>
       </mx:State>        
		<mx:State name="hideCntrlPanel">
			<mx:SetProperty target="{sideBar}" name="width" value="17"/>
			<mx:SetStyle target="{DivNavRB}" name="horizontalGap" value="0"/>
		</mx:State>
	</mx:states>
	<mx:transitions>
		<mx:Transition fromState="*" toState="*">
			<mx:Resize target="{sideBar}" />
		</mx:Transition>
	</mx:transitions>






</mx:Canvas>
